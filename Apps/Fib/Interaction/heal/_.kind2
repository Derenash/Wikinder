// Heal should
//  x  Increase HP
//  x  Return difference between old hp and new hp

#kdl_name = Fib_itr_heal
Apps.Fib.Interaction.heal (value: U120) (pos: Apps.Fib.Pos) : Apps.Fib.Interaction U120 {
  do Apps.Fib.Interaction {
    let func      = (player: Apps.Fib.Player) => Apps.Fib.Player.heal player value
    ask before    = Apps.Fib.Interaction.entity.get_at pos
                    Apps.Fib.Interaction.entity.mut_at func pos
    ask after     = Apps.Fib.Interaction.entity.get_at pos
    let real_heal = Apps.Fib.Player.hp.difference after before
    let health    = Apps.Fib.Player.hp.get after
                    Apps.Fib.Interaction.heal.apply_event real_heal health pos 
    return real_heal
  }
}

#kdl_name = Fib_itr_heae
Apps.Fib.Interaction.heal.apply_event (real_heal: U120) (health: U120) (pos: Apps.Fib.Pos) : Apps.Fib.Interaction Unit
Apps.Fib.Interaction.heal.apply_event (U120.new 0 0)  health pos = do Apps.Fib.Interaction {return Unit.new}
Apps.Fib.Interaction.heal.apply_event real_heal       health pos = 
  do Apps.Fib.Interaction {
    let event   = Apps.Fib.Event.heal pos real_heal
                  Apps.Fib.Interaction.event.add event
    return Unit.new 
  }
