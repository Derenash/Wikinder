#kdl_name = Fib_itr_mov
Apps.Fib.Interaction.move (pos: Apps.Fib.Pos) (delta: Pair U120 U120) : Apps.Fib.Interaction Bool {
  do Apps.Fib.Interaction {
    let destination = Apps.Fib.Pos.modify pos delta
    Apps.Fib.Interaction.move.maybe pos destination
  }
}

#kdl_name = Fib_itr_movm
Apps.Fib.Interaction.move.maybe (ori: Apps.Fib.Pos) (des: Maybe Apps.Fib.Pos) : Apps.Fib.Interaction Bool
Apps.Fib.Interaction.move.maybe ori (Maybe.none t    ) = Apps.Fib.Interaction.pure Bool.false
Apps.Fib.Interaction.move.maybe ori (Maybe.some t des) =
  do Apps.Fib.Interaction {
    ask des_id      = Apps.Fib.Interaction.entity.get_id_at des
    ask ori_id      = Apps.Fib.Interaction.entity.get_id_at ori
    if (U120.greater_equal ori_id (U120.new 0 512))
      {Apps.Fib.Interaction.move.if des_id ori des}
    else 
      {Apps.Fib.Interaction.pure Bool.false}
  }

#kdl_name = Fib_itr_movi
Apps.Fib.Interaction.move.if (id: Apps.Fib.Id) (ori: Apps.Fib.Pos) (des: Apps.Fib.Pos): Apps.Fib.Interaction Bool
Apps.Fib.Interaction.move.if (U120.new 0 0) ori des =
  do Apps.Fib.Interaction {
    let func = (state: Apps.Fib.State) => Apps.Fib.State.player.move state ori des
    Apps.Fib.Interaction.state.mut func
    let event = Apps.Fib.Event.movement ( ori) ( des)
    Apps.Fib.Interaction.event.add event
    return Bool.true
  }
Apps.Fib.Interaction.move.if id ori des             = Apps.Fib.Interaction.pure Bool.false