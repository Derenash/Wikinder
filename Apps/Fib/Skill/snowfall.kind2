#kdl_name = Fib_skl_snwf
Apps.Fib.Skill.snowfall : Apps.Fib.Interaction Unit {
  do Apps.Fib.Interaction {

    // Skill Constants
    let skill_id = U120.new 0 6
    let damage   = U120.new 0 25
    let range    = U120.new 0 4 
    let area     = U120.new 0 1
    let speed    = U120.new 0 30
    let slow     = U120.new 0 20

    ask cooldown = Apps.Fib.Interaction.player.cooldown speed
    ask target   = Apps.Fib.Interaction.target.get

    // Verifies if the enemy is in range
    ask Apps.Fib.Interaction.skill.check_range range

    // Get the delta of all coordinates "3x3 no Edges" besides Center
    let deltas = Apps.Fib.Pos.Delta.cross area
    let tttne  = Apps.Fib.Pos.range target deltas
    let area   = List.cons target tttne

    // Damages and applies slow the units in the area
    let func = (
      (pos: Apps.Fib.Pos) =>
        do Apps.Fib.Interaction {
        ask Apps.Fib.Interaction.damage damage pos
        ask Apps.Fib.Interaction.player.add_slow_at slow pos
        return Unit.new
        }
      )
    ask Apps.Fib.Interaction.area.generic func area

    // Registers which skill has been used
    ask Apps.Fib.Interaction.skill.event_use skill_id

    //Adds Fatigue to the caster
    ask Apps.Fib.Interaction.player.cast_skill cooldown
    return Unit.new
  }
}
